# n -- is to work on the nth code block
# rel_path -- is the relative path to the file
# hide_codeblocks -- is a flag to hide the markup with a details tag

BEGIN {
  i=0;
  start_comment="<!-- generated by mermaid compile action - START -->";
  end_comment="<!-- generated by mermaid compile action - END -->";
  rel_link="![~mermaid diagram " n "~](" rel_path ")";
  abs_link="<!-- ![~mermaid diagram " n "~](" abs_path ") -->";
}

# Rules to erase old generated code

($0 ~ start_comment && i+1 == n) {in_generated_block=1; next}

($0 ~ end_comment && in_generated_block) {in_generated_block=0; next}

# increment the block counter

(/```mermaid/) {i++}

# Found the block to work on

(/```mermaid/ && i == n) {
  print start_comment;
  print rel_link;
  print abs_link;

  if (hide_codeblocks) {
    print "<details>";
    print "  <summary>Mermaid markup</summary>";
    details_tag_open=1;
  }

  print "";
  print;
  in_code_block=1;
  next;
}

(in_code_block && /```/) {
  print;
  print "";

  if (details_tag_open) {
    print "</details>";
    details_tag_open=0;
  }

  print end_comment;
  in_code_block=0;
  next;
}

(in_generated_block && !in_code_block) {next}

{print}
