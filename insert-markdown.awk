# n -- is to work on the nth code block
# rel_path -- is the relative path to the file
# codeblock_action -- what to do with the codeblock

BEGIN {
  i=0;
  start_comment="<!-- generated by mermaid compile action - START -->";
  end_comment="<!-- generated by mermaid compile action - END -->";
  link="![~mermaid diagram " n "~](" path ")";
  inline="<img src=\"" path "\">";
}

# Rules to erase old generated code

($0 ~ start_comment && i+1 == n) {in_generated_block=1; next}

($0 ~ end_comment && in_generated_block) {in_generated_block=0; next}

# increment the block counter

(/```mermaid/) {i++}

# Found the block to work on

(/```mermaid/ && i == n) {
  print start_comment;
  if (codeblock_action == "replace") {
    print inline;
  } else {
    print link;
  }

  if (codeblock_action == "hide") {
    print "<details>";
    print "  <summary>Mermaid markup</summary>";
    details_tag_open=1;
  }

  if (codeblock_action != "replace") {
    print "";
    print;
  }
  in_code_block=1;
  next;
}

(in_code_block && /```/) {
  if (codeblock_action != "replace") {
    print;
    print "";
  }

  if (details_tag_open) {
    print "</details>";
    details_tag_open=0;
  }

  print end_comment;
  in_code_block=0;
  next;
}

(in_generated_block && !in_code_block) {next}

(in_code_block && codeblock_action == "replace") {next}

{print}
